cmake_minimum_required(VERSION 3.2)
project(TestFindOpenGL C)
include(CTest)

if(CMake_TEST_FindOpenGL_REQUIRED)
  find_package(OpenGL REQUIRED)
else()
  find_package(OpenGL)
  if(NOT OPENGL_FOUND)
    message("OpenGL not found, skipping rest of test.")
    return()
  endif()
endif()

add_executable(test_opengl_tgt main.c)
if(TARGET OpenGL::GLU)
  target_link_libraries(test_opengl_tgt OpenGL::GLU)
  target_compile_definitions(test_opengl_tgt PRIVATE HAVE_GLU)
else()
  target_link_libraries(test_opengl_tgt OpenGL::GL)
endif()
add_test(NAME test_opengl_tgt COMMAND test_opengl_tgt)

add_executable(test_opengl_var main.c)
if(OPENGL_glu_LIBRARY)
  target_compile_definitions(test_opengl_var PRIVATE HAVE_GLU)
endif()
target_include_directories(test_opengl_var PRIVATE ${OPENGL_INCLUDE_DIR})
target_link_libraries(test_opengl_var PRIVATE ${OPENGL_LIBRARIES})
add_test(NAME test_opengl_var COMMAND test_opengl_var)
